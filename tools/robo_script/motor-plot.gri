.nargs. = {rpn argc}

if {rpn .nargs. 2 ==}
	\datafile1 = {rpn 1 argv}
else if {rpn .nargs. 3 ==}
    \datafile1 = {rpn 1 argv}
    \outname = {rpn 2 argv}
    set postscript filename \outname
else
	show "Error - invalid number of arguments!"
	show "Usage: gri " {rpn 0 argv} " <data1>"
	quit
end if

set x size 17
set y size 17
set font to Times
set x name "Time (seconds)"
set y name "Current (Amps)"

show "opening \datafile1..."
open \datafile1

# first do time vs amps
read columns x y * *
x *= 0.001

set color rgb 0 0 0
draw x axis
draw y axis left
draw title "Motor Data"


set color rgb 1 0 0
draw curve

# now running avg amps
rewind
read columns x * y *
x *= 0.001

set color rgb 0 0 1
draw axes none
draw curve overlying

# draw speed axes
set color rgb 0 0 0
set y name "Speed (metres/second)"
set y axis 0 0.8 0.1
draw y axis at right

# now draw speed:
rewind
read columns x * * y *
x *= 0.001

set color rgb 0 0 0
draw curve

close

