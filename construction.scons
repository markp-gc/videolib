import os
import compilers
import envBuilder
import utils

AddOption( '--build-for',
           dest='build-for',
           type='string',
           nargs=1,
           action='store',
           metavar='ARCHITECTURE',
           default='native',
           help='Specify architecture to build for: native,beagle,android')

AddOption( '--type',
           dest='build-type',
           type='string',
           nargs=1,
           action='store',
           metavar='BUILD CONFIGURATION',
           default='release',
           help='Specify configuration: debug,release')

# Determine build configuration from command line options:
target = GetOption('build-for')
buildType = GetOption('build-type')

if not utils.TargetIsValid(target):
    print 'Invalid target: ' + target
    Exit(1)

if not utils.BuildTypeIsValid(buildType):
    print 'Invalid build type: ' + buildType
    Exit(1)

# Setup the compiler:
compiler = compilers.makeCompilerFor(target, buildType)
compiler.AppendFlags( "-Wall -pedantic" )

# Initialise the environment to use this compiler:
env = envBuilder.makeEnvForCompiler(compiler)

env.Decider('MD5-timestamp')

# Make path for build directory:
buildDir = os.path.join( 'build', target, buildType )

subdirs = utils.FindSconsDirs( './' )
for sconsDir in subdirs:
    sconsFile = os.path.join( sconsDir, 'SConscript' )
    buildPath = os.path.join( 'build', sconsDir, target, buildType )
    clonedEnv = env.Clone()
    SConscript( sconsFile, variant_dir=buildPath, duplicate=0, exports={ 'env' : clonedEnv, 'target' : target, 'compiler' : compiler } )

